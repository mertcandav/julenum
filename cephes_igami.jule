// Copyright 2025 mertcandav.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Derived from SciPy's special/cephes/igami.c
// https://github.com/scipy/scipy/blob/master/scipy/special/cephes/igami.c
// Made freely available by Stephen L. Moshier without support or guarantee.

// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.
// Copyright ©1984, ©1987, ©1995 by Stephen L. Moshier
// Portions Copyright ©2017 The Gonum Authors. All rights reserved.

use "std/math"

// Computes the inverse of the incomplete Gamma function. That is, it
// returns the x such that:
//
//	_IgamC(a, x) = p
//
// The input argument a must be positive and p must be between 0 and 1
// inclusive or it will panic. It should return a positive number, but
// can return 0 even with non-zero y due to underflow.
fn _IgamI(a: f64, p: f64): f64 {
	// Bound the solution
	mut x0 := f64.Max
	mut yl := 0.0
	mut x1 := 0.0
	mut yh := 1.0
	dithresh := 5.0 * machEp

	if p < 0 || p > 1 || a <= 0 {
		panic(paramOutOfBounds)
	}

	if p == 0 {
		ret math::Inf(1)
	}

	if p == 1 {
		ret 0.0
	}

	// Starting with the approximate value
	//  x = a y^3
	// where
	//  y = 1 - d - ndtri(p) sqrt(d)
	// and
	//  d = 1/9a
	// the routine performs up to 10 Newton iterations to find the root of
	//  _IgamC(a, x) - p = 0
	mut d := 1.0 / (9.0 * a)
	mut y := 1.0 - d - ndtri(p)*math::Sqrt(d)
	mut x := a * y * y * y

	mut lgm := lgam(a)

	mut i := 0
	for i < 10; i++ {
		if x > x0 || x < x1 {
			break
		}

		y = _IgamC(a, x)

		if y < yl || y > yh {
			break
		}

		if y < p {
			x0 = x
			yl = y
		} else {
			x1 = x
			yh = y
		}

		// Compute the derivative of the function at this point
		d = (a-1)*math::Log(x) - x - lgm
		if d < -maxLog {
			break
		}
		d = -math::Exp(d)

		// Compute the step to the next approximation of x
		d = (y - p) / d
		if math::Abs(d/x) < machEp {
			ret x
		}
		x = x - d
	}

	d = 0.0625
	if x0 == f64.Max {
		if x <= 0 {
			x = 1
		}
		for x0 == f64.Max {
			x = (1 + d) * x
			y = _IgamC(a, x)
			if y < p {
				x0 = x
				yl = y
				break
			}
			d = d + d
		}
	}

	d = 0.5
	mut dir := 0
	i = 0
	for i < 400; i++ {
		x = x1 + d*(x0-x1)
		y = _IgamC(a, x)

		lgm = (x0 - x1) / (x1 + x0)
		if math::Abs(lgm) < dithresh {
			break
		}

		lgm = (y - p) / p
		if math::Abs(lgm) < dithresh {
			break
		}

		if x <= 0 {
			break
		}

		if y >= p {
			x1 = x
			yh = y
			if dir < 0 {
				dir = 0
				d = 0.5
			} else if dir > 1 {
				d = 0.5*d + 0.5
			} else {
				d = (p - yl) / (yh - yl)
			}
			dir++
		} else {
			x0 = x
			yl = y
			if dir > 0 {
				dir = 0
				d = 0.5
			} else if dir < -1 {
				d = 0.5 * d
			} else {
				d = (p - yl) / (yh - yl)
			}
			dir--
		}
	}

	ret x
}