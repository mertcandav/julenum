// Copyright 2025 mertcandav.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Implementation derived from the test implementation of the Jule's standard math package.
//
// Copyright 2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/math"
use "std/math/cmplx"

// Reports whether two values a and b are approximately equal,
// within a relative tolerance e. This is useful for comparing floating-point values
// where exact equality is unreliable due to rounding errors.
//
// It returns true if the absolute difference between a and b
// is less than the tolerance threshold scaled by the expected value b,
// or if a and b are exactly equal.
fn Tolerance(a: f64, b: f64, mut e: f64): bool {
	// Multiplying by e here can underflow denormal values to zero.
	// Check a==b so that at least if a and b are small and identical
	// we say they match.
	if a == b {
		ret true
	}
	mut d := a - b
	if d < 0 {
		d = -d
	}

	// note: b is correct (expected) value, a is actual value.
	// make error tolerance a fraction of b, not a.
	if b != 0 {
		e = e * b
		if e < 0 {
			e = -e
		}
	}
	ret d < e
}

// Reports whether two values a and b are approximately equal
// within a very tight default relative tolerance of 1e-14.
fn Close(a: f64, b: f64): bool { ret Tolerance(a, b, 1e-14) }

// Reports whether two values a and b are approximately equal
// within an extremely tight default relative tolerance of 4e-16.
fn VeryClose(a: f64, b: f64): bool { ret Tolerance(a, b, 4e-16) }

// Reports whether two values a and b are considered alike.
// It returns true if both are NaN (Not a Number), or if they are exactly equal
// including matching their sign bits (distinguishing +0 and -0).
fn Alike(a: f64, b: f64): bool {
	match {
	| math::IsNaN(a) && math::IsNaN(b):
		ret true
	| a == b:
		ret math::Signbit(a) == math::Signbit(b)
	}
	ret false
}

// Reports whether two values a and b are approximately equal,
// within a relative tolerance e. This is useful for comparing floating-point values
// where exact equality is unreliable due to rounding errors.
//
// It returns true if the absolute difference between a and b
// is less than the tolerance threshold scaled by the expected value b,
// or if a and b are exactly equal.
fn CmplxTolerance(a: cmplx128, b: cmplx128, mut e: f64): bool {
	d := cmplx::Abs(a - b)
	if b != 0 {
		e = e * cmplx::Abs(b)
		if e < 0 {
			e = -e
		}
	}
	ret d < e
}

// Reports whether two values a and b are approximately equal
// within a very tight default relative tolerance of 1e-14.
fn CmplxClose(a: cmplx128, b: cmplx128): bool { ret CmplxTolerance(a, b, 1e-14) }