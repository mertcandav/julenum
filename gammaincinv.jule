// Copyright 2025 mertcandav.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Derived from SciPy's special/c_misc/gammaincinv.c
// https://github.com/scipy/scipy/blob/master/scipy/special/c_misc/gammaincinv.c

// Implementation derived from the gonum.
//
// Copyright Â©2017 The Gonum Authors. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

use "std/math"

const (
	allowedATol = 1e-306
	allowedRTol = 1e-6
)

fn gammaIncReg(x: f64, params: []f64): f64 {
	ret _Igam(params[0], x) - params[1]
}

// Inverse of the regularized incomplete Gamma integral. That is, it returns x such that:
//
//	Igam(a, x) = y
//
// The input argument a must be positive and y must be between 0 and 1
// inclusive or it will panic. It should return a
// positive number, but can return NaN if there is a failure to converge.
fn gammaIncRegInv(a: f64, y: f64): f64 {
	// For y not small, we just use
	//  _IgamI(a, 1-y)
	// (inverse of the complemented incomplete Gamma integral). For y small,
	// however, 1-y is about 1, and we lose digits.
	if a <= 0 || y <= 0 || y >= 0.25 {
		ret _IgamI(a, 1-y)
	}

	mut lo := 0.0
	mut flo := -y
	mut hi := _IgamI(a, 0.75)
	mut fhi := 0.25 - y

	params := [a, y]

	// Also, after we generate a small interval by bisection above, false
	// position will do a large step from an interval of width ~1e-4 to ~1e-14
	// in one step (a=10, x=0.05, but similar for other values).
	result, mut bestX, _, errEst := falsePosition(lo, hi, flo, fhi, 2*machEp, 2*machEp, 1e-2*a, gammaIncReg, params)
	if result == fSolveMaxIterations && errEst > allowedATol+allowedRTol*math::Abs(bestX) {
		bestX = math::NaN()
	}

	ret bestX
}