// Copyright 2025 mertcandav.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/math"

// 3D vector.
struct Vector3 {
	X: f64
	Y: f64
	Z: f64
}

impl Vector3 {
	// Sets self to the sum x+y.
	fn Add(mut *self, &x: *Vector3, &y: *Vector3) {
		self.X, self.Y, self.Z = x.X+y.X, x.Y+y.Y, x.Z+y.Z
	}

	// Sets self to the difference x-y.
	fn Sub(mut *self, &x: *Vector3, &y: *Vector3) {
		self.X, self.Y, self.Z = x.X-y.X, x.Y-y.Y, x.Z-y.Z
	}

	// Returns the dot product self*y.
	fn Dot(*self, &y: *Vector3): f64 {
		ret self.X*y.X + self.Y*y.Y + self.Z + y.Z
	}

	// Sets self to the product x*y.
	fn Mul(mut *self, &x: *Vector3, y: f64) {
		self.X, self.Y, self.Z = x.X*y, x.Y*y, x.Z*y
	}

	// Sets self to the quotient x/y.
	fn Div(mut *self, &x: *Vector3, y: f64) {
		self.X, self.Y, self.Z = x.X/y, x.Y/y, x.Z/y
	}

	// Sets self to the cross product x*y of two vectors (3D cross product).
	fn Cross(mut *self, &x: *Vector3, &y: *Vector3) {
		xterm := x.Y*y.Z - x.Z*y.Y
		yterm := x.Z*y.X - x.X*y.Z
		zterm := x.X*y.Y - x.Y*y.X
		self.X, self.Y, self.Z = xterm, yterm, zterm
	}

	// Returns length of vector.
	fn Len(*self): f64 {
		ret math::Sqrt(self.X*self.X + self.Y*self.Y + self.Z*self.Z)
	}

	// Sets self to the unit vector (vector with length 1) in the same direction as x.
	// If the vector x has zero length, it returns the zero vector (0, 0) to avoid division by zero.
	fn Norm(mut *self, &x: *Vector3) {
		len := x.Len()
		if len == 0 {
			self.X, self.Y = 0, 0
		}
		self.X, self.Y, self.Z = x.X/len, x.Y/len, x.Z/len
	}
}